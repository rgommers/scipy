fortran_ignore_warnings = [
  '-Wno-tabs', '-Wno-conversion',
  '-Wno-argument-mismatch', '-Wno-unused-dummy-argument',
  '-Wno-maybe-uninitialized', '-Wno-unused-label',
  '-Wno-unused-variable']

fitpack_src = ['fitpack/fpclos.f',
  'fitpack/sphere.f',
  'fitpack/cocosp.f',
  'fitpack/fpched.f',
  'fitpack/fppocu.f',
  'fitpack/fprota.f',
  'fitpack/fppara.f',
  'fitpack/fpcyt1.f',
  'fitpack/fpgivs.f',
  'fitpack/concon.f',
  'fitpack/fpperi.f',
  'fitpack/curev.f',
  'fitpack/concur.f',
  'fitpack/fpcuro.f',
  'fitpack/parsur.f',
  'fitpack/bispev.f',
  'fitpack/parcur.f',
  'fitpack/fpknot.f',
  'fitpack/fprpsp.f',
  'fitpack/fppasu.f',
  'fitpack/fpsuev.f',
  'fitpack/fpgrdi.f',
  'fitpack/fpback.f',
  'fitpack/surfit.f',
  'fitpack/fpsphe.f',
  'fitpack/fpdeno.f',
  'fitpack/fppogr.f',
  'fitpack/fpsurf.f',
  'fitpack/fptrpe.f',
  'fitpack/fpcosp.f',
  'fitpack/splint.f',
  'fitpack/fpgrre.f',
  'fitpack/fpfrno.f',
  'fitpack/fprppo.f',
  'fitpack/parder.f',
  'fitpack/fpseno.f',
  'fitpack/fpintb.f',
  'fitpack/fpsysy.f',
  'fitpack/fporde.f',
  'fitpack/fpcyt2.f',
  'fitpack/fpgrsp.f',
  'fitpack/fpgrpa.f',
  'fitpack/fpchep.f',
  'fitpack/surev.f',
  'fitpack/fpcons.f',
  'fitpack/polar.f',
  'fitpack/fpinst.f',
  'fitpack/fpbacp.f',
  'fitpack/fpregr.f',
  'fitpack/fpspgr.f',
  'fitpack/fpadno.f',
  'fitpack/fpcsin.f',
  'fitpack/fpdisc.f',
  'fitpack/splev.f',
  'fitpack/fptrnp.f',
  'fitpack/fourco.f',
  'fitpack/dblint.f',
  'fitpack/pardeu.f',
  'fitpack/fpbspl.f',
  'fitpack/fprank.f',
  'fitpack/bispeu.f',
  'fitpack/fpbisp.f',
  'fitpack/clocur.f',
  'fitpack/fpcoco.f',
  'fitpack/pogrid.f',
  'fitpack/fppola.f',
  'fitpack/spalde.f',
  'fitpack/fpopdi.f',
  'fitpack/sproot.f',
  'fitpack/fpader.f',
  'fitpack/fpadpo.f',
  'fitpack/fpcurf.f',
  'fitpack/splder.f',
  'fitpack/fpbfout.f',
  'fitpack/profil.f',
  'fitpack/fpchec.f',
  'fitpack/curfit.f',
  'fitpack/regrid.f',
  'fitpack/fprati.f',
  'fitpack/percur.f',
  'fitpack/fpopsp.f',
  'fitpack/evapol.f',
  'fitpack/insert.f',
  'fitpack/spgrid.f',
  'fitpack/cualde.f']

# TODO: Add flags for 64 bit ints
fitpack_lib = static_library('fitpack_lib',
  fitpack_src)

interpnd = py3.extension_module('interpnd',
  ['interpnd.pyx'],
  c_args: numpy_nodepr_api,
  fortran_args: fortran_ignore_warnings,
  include_directories: [incdir_numpy],
  dependencies: [py3_dep],
  install : true,
  subdir : 'scipy/interpolate')

_ppoly = py3.extension_module('_ppoly',
  ['_ppoly.pyx'],
  c_args: numpy_nodepr_api,
  include_directories: [incdir_numpy],
  dependencies: [py3_dep],
  install : true,
  subdir : 'scipy/interpolate')

_bspl = py3.extension_module('_bspl',
  ['_bspl.pyx'],
  c_args: numpy_nodepr_api,
  include_directories: ['src/', incdir_numpy],
  dependencies: [py3_dep],
  install : true,
  subdir : 'scipy/interpolate')

# TODO: Add flags for 64 bit ints
_fitpack = py3.extension_module('_fitpack',
  ['src/_fitpackmodule.c'],
  c_args: ['-Wno-cpp', '-Wno-maybe-uninitialized'], # TODO: Replace with numpy_nodepr_api
  link_with: [fitpack_lib],
  include_directories: ['src/', incdir_numpy],
  dependencies: [py3_dep],
  install : true,
  subdir : 'scipy/interpolate')

dfitpack_module = custom_target('dfitpack_module',
  output : ['dfitpack-f2pywrappers.f', 'dfitpackmodule.c'],
  input : 'src/fitpack.pyf',
  command : [py3, generate_f2pymod, '@INPUT@', '-o', '@OUTDIR@'],
)

# TODO: Add flags for 64 bit ints
dfitpack = py3.extension_module('dfitpack',
  [dfitpack_module, fortranobject_c],
  c_args: [numpy_nodepr_api, '-Wno-unused-variable'],
  include_directories: [incdir_numpy, incdir_f2py],
  dependencies: [py3_dep, lapack],
  link_with: [fitpack_lib],
  install: true,
  subdir: 'scipy/interpolate')

if use_pythran
  _rbfinterp_pythran = custom_target('_rbfinterp_pythran',
    output: ['_rbfinterp_pythran.cpp'],
    input : '_rbfinterp_pythran.py',
    command : [pythran, '-E', '@INPUT@', '-o', '@OUTDIR@/_rbfinterp_pythran.cpp'],
  )
  _rbfinterp_pythran = py3.extension_module('_rbfinterp_pythran',
  [_rbfinterp_pythran],
  cpp_args: ['-Wno-unused-function', '-Wno-unused-variable', 
             '-Wno-deprecated-declarations',
             '-Wno-cpp', '-Wno-int-in-bool-context',
             '-Wno-unused-but-set-variable'
            ] + cpp_args_pythran,
  include_directories: [incdir_pythran, incdir_numpy],
  dependencies: [py3_dep],
  install: true,
  subdir: 'scipy/interpolate')
endif

py3.install_sources(['interpnd_info.py',
  '__init__.py',
  '_fitpack_impl.py',
  '_rbfinterp.py',
  '_cubic.py',
  'polyint.py',
  'fitpack.py',
  'ndgriddata.py',
  'fitpack2.py',
  '_bsplines.py',
  '_rbfinterp_pythran.py',
  'rbf.py',
  'interpolate.py',
  '_pade.py'],
  pure : false,
  subdir : 'scipy/interpolate'
)

subdir('tests')